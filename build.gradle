import com.sun.xml.internal.ws.util.xml.XmlUtil

plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id "org.embulk.embulk-plugins" version "0.4.2"
    id "java"
    id "maven-publish"
    id "signing"
    id "checkstyle"
}

repositories {
    mavenCentral()
    jcenter()

    maven {
        url "https://packages.confluent.io/maven/"
    }
}

group = "io.github.joker1007"
version = "0.3.0"
description = "Embulk output plugin for Apache Kafka"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.31"
    compileOnly "org.embulk:embulk-spi:0.10.31"

    testCompile "junit:junit:4.13.2"
    testCompile "org.embulk:embulk-api:0.10.31"
    testCompile "org.embulk:embulk-spi:0.10.31"
    testCompile "org.embulk:embulk-core:0.10.31"
    testCompile "org.embulk:embulk-core:0.10.31:tests"
    testCompile "org.embulk:embulk-junit4:0.10.31"
    testCompile "org.embulk:embulk-input-file:0.10.31"
    testCompile "org.embulk:embulk-parser-csv:0.10.31"
    testCompile "org.embulk:embulk-deps:0.10.31"

    compile("org.embulk:embulk-util-config:0.2.1") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    compile "com.fasterxml.jackson.core:jackson-annotations:2.13.1"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"

    compile("org.apache.kafka:kafka-clients:2.7.0") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    compile("org.apache.avro:avro:1.10.0") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    compile("io.confluent:kafka-avro-serializer:6.1.0") {
        exclude group: "org.apache.kafka", module: "kafka-clients"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    testCompile "com.salesforce.kafka.test:kafka-junit4:3.+"
    testCompile("org.apache.kafka:kafka_2.12:2.7.+") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
//        exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-csv"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from rootProject.file("LICENSE.txt")
}

javadocJar {
    from rootProject.file("LICENSE.txt")
}

sourcesJar {
    from rootProject.file("LICENSE.txt")
}


embulkPlugin {
    mainClass = "org.embulk.output.kafka.KafkaOutputPlugin"
    category = "output"
    type = "kafka"
}

test {
    minHeapSize = "512m"
    maxHeapSize = "4g"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

task generateEmbulkProperties {
    doLast {
        mkdir ".embulk"
        def f = file(".embulk/embulk.properties")
        f.write("m2_repo=${System.properties["user.home"]}/.m2/repository\nplugins.output.kafka=maven:${project.group}:kafka:${project.version}")
    }
}

gem {
    from("LICENSE.txt")
    authors = [ "joker1007" ]
    email = [ "kakyoin.hierophant@gmail.com" ]
    description = "Kafka output plugin for Embulk"
    summary = "Kafka output plugin for Embulk"
    homepage = "https://github.com/joker1007/embulk-output-kafka"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
            pom {
                name = "embulk-output-kafka"
                packaging = "jar"
                description = "Embulk output plugin for Apache Kafka"
                url = "https://github.com/joker1007/embulk-output-kafka"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                scm {
                    connection = "scm:git://github.com/joker1007/embulk-output-kafka.git"
                    developerConnection = "scm:git@github.com:joker1007/embulk-output-kafka.git"
                    url = "https://github.com/joker1007/embulk-output-kafka"
                }

                developers {
                    developer {
                        id = "joker1007"
                        name = "Tomohiro Hashidate"
                        email = "kakyoin.hierophant@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.embulkPluginMaven
}

tasks.withType(Sign) {
    onlyIf { System.getenv().get("SKIP_SIGNING") == null }
}

tasks.withType(GenerateMavenPom) {
    doLast {
        def f = file("${buildDir.name}/publications/embulkPluginMaven/pom-default.xml")
        def parser = new XmlParser()
        def xml = parser.parse(f)
        def repos = "<repositories>\n" +
                    "  <repository>\n" +
                    "    <id>confluent</id>\n" +
                    "    <url>https://packages.confluent.io/maven/</url>\n" +
                    "  </repository>\n" +
                    "</repositories>\n"
        def reposNode = parser.parseText(repos)
        xml.append(reposNode)
        f.write(groovy.xml.XmlUtil.serialize(xml))
    }
}