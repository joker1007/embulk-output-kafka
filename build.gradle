import groovy.xml.XmlUtil
import groovy.xml.XmlParser

plugins {
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "java"
    id "maven-publish"
    id "signing"
}

repositories {
    mavenCentral()

    maven {
        url "https://packages.confluent.io/maven/"
    }
}

group = "io.github.joker1007"
version = "0.3.1"
description = "Embulk output plugin for Apache Kafka"

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.43"
    compileOnly "org.embulk:embulk-spi:0.11"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.embulk:embulk-api:0.10.43"
    testImplementation "org.embulk:embulk-spi:0.11"
    testImplementation "org.embulk:embulk-core:0.11.0"
    testImplementation "org.embulk:embulk-junit4:0.11.0"
    testImplementation "org.embulk:embulk-input-file:0.11.0"
    testImplementation "org.embulk:embulk-parser-csv:0.11.0"
    testImplementation "org.embulk:embulk-deps:0.11.0"

    implementation("org.embulk:embulk-util-config:0.3.1") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    implementation "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"

    implementation("org.apache.kafka:kafka-clients:2.8.2") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    implementation("org.apache.avro:avro:1.11.1") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    implementation("io.confluent:kafka-avro-serializer:6.2.2") {
        exclude group: "org.apache.kafka", module: "kafka-clients"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    testImplementation "com.salesforce.kafka.test:kafka-junit4:3.+"
    testImplementation("org.apache.kafka:kafka_2.12:2.8.+") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from rootProject.file("LICENSE.txt")
}

javadocJar {
    from rootProject.file("LICENSE.txt")
}

sourcesJar {
    from rootProject.file("LICENSE.txt")
}


embulkPlugin {
    mainClass = "org.embulk.output.kafka.KafkaOutputPlugin"
    category = "output"
    type = "kafka"
}

test {
    minHeapSize = "512m"
    maxHeapSize = "4g"
}

task generateEmbulkProperties {
    doLast {
        mkdir ".embulk"
        def f = file(".embulk/embulk.properties")
        f.write("m2_repo=${System.properties["user.home"]}/.m2/repository\nplugins.output.kafka=maven:${project.group}:kafka:${project.version}")
    }
}

gem {
    from("LICENSE.txt")
    authors = ["joker1007"]
    email = ["kakyoin.hierophant@gmail.com"]
    description = "Kafka output plugin for Embulk"
    summary = "Kafka output plugin for Embulk"
    homepage = "https://github.com/joker1007/embulk-output-kafka"
    licenses = ["MIT"]
}

gemPush {
    host = "https://rubygems.org"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
            pom {
                name = "embulk-output-kafka"
                packaging = "jar"
                description = "Embulk output plugin for Apache Kafka"
                url = "https://github.com/joker1007/embulk-output-kafka"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                scm {
                    connection = "scm:git://github.com/joker1007/embulk-output-kafka.git"
                    developerConnection = "scm:git@github.com:joker1007/embulk-output-kafka.git"
                    url = "https://github.com/joker1007/embulk-output-kafka"
                }

                developers {
                    developer {
                        id = "joker1007"
                        name = "Tomohiro Hashidate"
                        email = "kakyoin.hierophant@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.embulkPluginMaven
}

tasks.withType(Sign) {
    onlyIf { System.getenv().get("SKIP_SIGNING") == null }
}

tasks.withType(GenerateMavenPom) {
    doLast {
        def f = file(layout.buildDirectory.file("publications/embulkPluginMaven/pom-default.xml"))
        XmlParser parser = new XmlParser()
        def xml = parser.parse(f)
        def repos = "<repositories>\n" +
                "  <repository>\n" +
                "    <id>confluent</id>\n" +
                "    <url>https://packages.confluent.io/maven/</url>\n" +
                "  </repository>\n" +
                "</repositories>\n"
        def reposNode = parser.parseText(repos)
        xml.append(reposNode)
        f.write(XmlUtil.serialize(xml))
    }
}
